plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.alibaba.arouter'
    id 'kotlin-kapt'
}
apply from: '../gradle/appProductFlavors.gradle'
apply from: '../gradle/monitor.gradle'

android {
    compileSdk rootProject.ext.android.compileSdk

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdk rootProject.ext.android.minSdk
        targetSdk rootProject.ext.android.targetSdk
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        // 仅保留中文语种的资源
        resConfigs 'zh'
        //必须要保证所有的flavor 都属于同一个维度
        flavorDimensions "default"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    dataBinding {
        enabled true
    }
    viewBinding {
        enabled true
    }
    //代码检查
    lintOptions {
        abortOnError false
        disable 'InconsistentArrays', 'DefaultLocale', 'OldTargetApi', 'GradleOverrides', 'MissingTranslation', 'UnusedResources', 'GoogleAppIndexingWarning'
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    packagingOptions {
        // 剔除这个包下的所有文件（不会移除签名信息）
        exclude 'junit/runner/*'
        exclude 'LICENSE-junit.txt'
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    kapt 'com.alibaba:arouter-compiler:1.5.2'

    implementation project(':lib:lib_aar')
    implementation project(':module:module_main')
    implementation project(':module:module_home')
}