// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    // Studio 插件推荐：https://github.com/getActivity/StudioPlugins
    dependencies {
        //分享sdk集成步骤1：在项目Gradle文件中注册MobSDK https://mob.com/wiki/detailed?wiki=9&id=14
        classpath "com.mob.sdk:MobSDK:2018.0319.1724"
        //注入框架 https://dagger.dev/
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.44.2'
        //https://github.com/luckybilly/AutoRegister  https://repo1.maven.org/maven2/com/alibaba/arouter-register/
        classpath "com.alibaba:arouter-register:1.0.2"
        // 基于AndroidLocalService实现的抓取OKHTTP请求的工具 https://github.com/lygttpod/AndroidMonitor
        classpath 'io.github.lygttpod:monitor-plugin:0.0.2'
        // 将项目发布到JCenter 所需要的jar   添加依赖
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
    }
}
plugins {
    id 'com.android.application' version '7.2.2' apply false
    id 'com.android.library' version '7.2.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.20' apply false
}
subprojects {
    project.configurations.all {
        resolutionStrategy.eachDependency { details ->
            // 版本对齐，不然会自动升级
            if (details.requested.group == 'androidx.lifecycle') {
                details.useVersion sdkVersion.lifecycle
            }
        }
    }
}
task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: "$rootDir\\gradle\\config.gradle"
Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
rootProject.ext.config.localProject = localProperties.getProperty("local.project") == 'true'
// 配置每个库都有发布脚本，可以发布到远程仓库
def publishModules = ['lib_aar', 'lib_base', 'lib_bridge', 'lib_net', 'lib_resource', 'lib_widget']
rootProject.getSubprojects().forEach { subproject ->
    subproject.afterEvaluate {
        if (publishModules.contains(subproject.name)) {
            project(subproject.name) {
                apply from: new File("$rootDir\\gradle", 'artifactory.gradle')
            }
        }
    }
}